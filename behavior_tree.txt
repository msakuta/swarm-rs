tree main = Sequence {
    if (!HasTarget) {
        FindEnemy
    }
    Fallback {
        ForceFailure {
            TargetPos (pos -> targetPos)
        }
        if (IsTargetVisible (target <- targetPos)) {
            FaceToTarget (target <- targetPos)
            Shoot
        } else {
            FollowPathAndAvoid
        }
    }
}

tree FollowPathAndAvoid = Sequence {
    TargetPos (pos -> target_pos)
    if (!HasPath) {
        FindPath (target <- target_pos)
    }
    if (IsTargetVisible (target <- target_pos)) {
        FaceToTarget (target <- target_pos)
        Shoot
    }
    if (HasPath) {
        SimpleAvoidance
        if (!FollowPath) {
            ReactiveSequence {
                Drive (direction <- "backward")
                Randomize (max <- "20", value -> timeoutValue)
                Timeout (time <- timeoutValue)
            }
            #AvoidancePlan
        }
        Shoot
    }
}

tree AvoidancePlan = Sequence {
    if (PathNextNode (output -> pathNext)) {
        Randomize (min <- "20", max <- "100", value -> timeoutValue)
        ReactiveFallback {
            Avoidance (goal <- pathNext)
            ForceFailure {
                Timeout (time <- timeoutValue)
            }
            ClearAvoidance
        }
    }
}
